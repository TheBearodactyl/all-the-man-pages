bigrat(3perl)                                                                        Perl Programmers Reference Guide                                                                        bigrat(3perl)

NAME
       bigrat - transparent big rational number support for Perl

SYNOPSIS
           use bigrat;

           print 2 + 4.5;                      # Math::BigRat 13/2
           print 1/3 + 1/4;                    # Math::BigRat 7/12
           print inf + 42;                     # Math::BigRat inf
           print NaN * 7;                      # Math::BigRat NaN
           print hex("0x1234567890123490");    # Perl v5.10.0 or later

           {
               no bigrat;
               print 1/3;                      # 0.33333...
           }

           # for older Perls, import into current package:
           use bigrat qw/hex oct/;
           print hex("0x1234567890123490");
           print oct("01234567890123490");

DESCRIPTION
       All numeric literal in the given scope are converted to Math::BigRat objects.

       All operators (including basic math operations) except the range operator ".."  are overloaded.

       So, the following:

           use bigrat;
           $x = 1234;

       creates a Math::BigRat and stores a reference to in $x. This happens transparently and behind your back, so to speak.

       You can see this with the following:

           perl -Mbigrat -le 'print ref(1234)'

       Since numbers are actually objects, you can call all the usual methods from Math::BigRat on them. This even works to some extent on expressions:

           perl -Mbigrat -le '$x = 1234; print $x->bdec()'
           perl -Mbigrat -le 'print 1234->copy()->binc();'
           perl -Mbigrat -le 'print 1234->copy()->binc->badd(6);'
           perl -Mbigrat -le 'print +(1234)->copy()->binc()'

       (Note that print doesn't do what you expect if the expression starts with '(' hence the "+")

       You can even chain the operations together as usual:

           perl -Mbigrat -le 'print 1234->copy()->binc->badd(6);'
           1241

       Please note the following does not work as expected (prints nothing), since overloading of '..' is not yet possible in Perl (as of v5.8.0):

           perl -Mbigrat -le 'for (1..2) { print ref($_); }'

   Options
       "bigrat" recognizes some options that can be passed while loading it via "use". The following options exist:

       a or accuracy
           This sets the accuracy for all math operations. The argument must be greater than or equal to zero. See Math::BigInt's bround() method for details.

               perl -Mbigrat=a,50 -le 'print sqrt(20)'

           Note that setting precision and accuracy at the same time is not possible.

       p or precision
           This sets the precision for all math operations. The argument can be any integer. Negative values mean a fixed number of digits after the dot, while a positive value rounds to this digit left
           from the dot. 0 means round to integer.  See Math::BigInt's bfround() method for details.

               perl -Mbigrat=p,-50 -le 'print sqrt(20)'

           Note that setting precision and accuracy at the same time is not possible.

       t or trace
           This enables a trace mode and is primarily for debugging.

       l, lib, try, or only
           Load a different math lib, see "Math Library".

               perl -Mbigrat=l,GMP -e 'print 2 ** 512'
               perl -Mbigrat=lib,GMP -e 'print 2 ** 512'
               perl -Mbigrat=try,GMP -e 'print 2 ** 512'
               perl -Mbigrat=only,GMP -e 'print 2 ** 512'

       hex Override the built-in hex() method with a version that can handle big numbers.  This overrides it by exporting it to the current package. Under Perl v5.10.0 and higher, this is not so
           necessary, as hex() is lexically overridden in the current scope whenever the "bigrat" pragma is active.

       oct Override the built-in oct() method with a version that can handle big numbers.  This overrides it by exporting it to the current package. Under Perl v5.10.0 and higher, this is not so
           necessary, as oct() is lexically overridden in the current scope whenever the "bigrat" pragma is active.

       v or version
           this prints out the name and version of the modules and then exits.

               perl -Mbigrat=v

   Math Library
       Math with the numbers is done (by default) by a backend library module called Math::BigInt::Calc. The default is equivalent to saying:

           use bigrat lib => 'Calc';

       you can change this by using:

           use bigrat lib => 'GMP';

       The following would first try to find Math::BigInt::Foo, then Math::BigInt::Bar, and if this also fails, revert to Math::BigInt::Calc:

           use bigrat lib => 'Foo,Math::BigInt::Bar';

       Using c<lib> warns if none of the specified libraries can be found and Math::BigInt fell back to one of the default libraries. To suppress this warning, use c<try> instead:

           use bigrat try => 'GMP';

       If you want the code to die instead of falling back, use "only" instead:

           use bigrat only => 'GMP';

       Please see the respective module documentation for further details.

   Method calls
       Since all numbers are now objects, you can use all methods that are part of the Math::BigRat API.

       But a warning is in order. When using the following to make a copy of a number, only a shallow copy will be made.

           $x = 9; $y = $x;
           $x = $y = 7;

       Using the copy or the original with overloaded math is okay, e.g., the following work:

           $x = 9; $y = $x;
           print $x + 1, " ", $y,"\n";     # prints 10 9

       but calling any method that modifies the number directly will result in both the original and the copy being destroyed:

           $x = 9; $y = $x;
           print $x->badd(1), " ", $y,"\n";        # prints 10 10

           $x = 9; $y = $x;
           print $x->binc(1), " ", $y,"\n";        # prints 10 10

           $x = 9; $y = $x;
           print $x->bmul(2), " ", $y,"\n";        # prints 18 18

       Using methods that do not modify, but test that the contents works:

           $x = 9; $y = $x;
           $z = 9 if $x->is_zero();                # works fine

       See the documentation about the copy constructor and "=" in overload, as well as the documentation in Math::BigFloat for further details.

   Methods
       inf()
           A shortcut to return Math::BigRat->binf(). Useful because Perl does not always handle bareword "inf" properly.

       NaN()
           A shortcut to return Math::BigRat->bnan(). Useful because Perl does not always handle bareword "NaN" properly.

       e
               # perl -Mbigrat=e -wle 'print e'

           Returns Euler's number "e", aka exp(1).

       PI
               # perl -Mbigrat=PI -wle 'print PI'

           Returns PI.

       bexp()
               bexp($power, $accuracy);

           Returns Euler's number "e" raised to the appropriate power, to the wanted accuracy.

           Example:

               # perl -Mbigrat=bexp -wle 'print bexp(1,80)'

       bpi()
               bpi($accuracy);

           Returns PI to the wanted accuracy.

           Example:

               # perl -Mbigrat=bpi -wle 'print bpi(80)'

       accuracy()
           Set or get the accuracy.

       precision()
           Set or get the precision.

       round_mode()
           Set or get the rounding mode.

       div_scale()
           Set or get the division scale.

       in_effect()
               use bigrat;

               print "in effect\n" if bigrat::in_effect;       # true
               {
                   no bigrat;
                   print "in effect\n" if bigrat::in_effect;   # false
               }

           Returns true or false if "bigrat" is in effect in the current scope.

           This method only works on Perl v5.9.4 or later.

CAVEATS
       Hexadecimal, octal, and binary floating point literals
           Perl (and this module) accepts hexadecimal, octal, and binary floating point literals, but use them with care with Perl versions before v5.32.0, because some versions of Perl silently give
           the wrong result.

       Operator vs literal overloading
           "bigrat" works by overloading handling of integer and floating point literals, converting them to Math::BigRat objects.

           This means that arithmetic involving only string values or string literals are performed using Perl's built-in operators.

           For example:

               use bigrat;
               my $x = "900000000000000009";
               my $y = "900000000000000007";
               print $x - $y;

           outputs 0 on default 32-bit builds, since "bigrat" never sees the string literals. To ensure the expression is all treated as "Math::BigRat" objects, use a literal number in the expression:

               print +(0+$x) - $y;

       Ranges
           Perl does not allow overloading of ranges, so you can neither safely use ranges with "bigrat" endpoints, nor is the iterator variable a "Math::BigRat".

               use 5.010;
               for my $i (12..13) {
                 for my $j (20..21) {
                   say $i ** $j;  # produces a floating-point number,
                                  # not an object
                 }
               }

       in_effect()
           This method only works on Perl v5.9.4 or later.

       hex()/oct()
           "bigrat" overrides these routines with versions that can also handle big integer values. Under Perl prior to version v5.9.4, however, this will not happen unless you specifically ask for it
           with the two import tags "hex" and "oct" - and then it will be global and cannot be disabled inside a scope with "no bigrat":

               use bigrat qw/hex oct/;

               print hex("0x1234567890123456");
               {
                   no bigrat;
                   print hex("0x1234567890123456");
               }

           The second call to hex() will warn about a non-portable constant.

           Compare this to:

               use bigrat;

               # will warn only under Perl older than v5.9.4
               print hex("0x1234567890123456");

EXAMPLES
           perl -Mbigrat -le 'print sqrt(33)'
           perl -Mbigrat -le 'print 2**255'
           perl -Mbigrat -le 'print 4.5+2**255'
           perl -Mbigrat -le 'print 3/7 + 5/7 + 8/3'
           perl -Mbigrat -le 'print 12->is_odd()';
           perl -Mbigrat=l,GMP -le 'print 7 ** 7777'

BUGS
       Please report any bugs or feature requests to "bug-bignum at rt.cpan.org", or through the web interface at <https://rt.cpan.org/Ticket/Create.html?Queue=bignum> (requires login).  We will be
       notified, and then you'll automatically be notified of progress on your bug as I make changes.

SUPPORT
       You can find documentation for this module with the perldoc command.

           perldoc bigrat

       You can also look for information at:

       •   GitHub

           <https://github.com/pjacklam/p5-bignum>

       •   RT: CPAN's request tracker

           <https://rt.cpan.org/Dist/Display.html?Name=bignum>

       •   MetaCPAN

           <https://metacpan.org/release/bignum>

       •   CPAN Testers Matrix

           <http://matrix.cpantesters.org/?dist=bignum>

       •   CPAN Ratings

           <https://cpanratings.perl.org/dist/bignum>

LICENSE
       This program is free software; you may redistribute it and/or modify it under the same terms as Perl itself.

SEE ALSO
       bignum and bigint.

       Math::BigInt, Math::BigFloat, Math::BigRat and Math::Big as well as Math::BigInt::FastCalc, Math::BigInt::Pari and Math::BigInt::GMP.

AUTHORS
       •   (C) by Tels <http://bloodgate.com/> in early 2002 - 2007.

       •   Maintained by Peter John Acklam <pjacklam@gmail.com>, 2014-.

perl v5.36.0                                                                                    2022-12-12                                                                                   bigrat(3perl)
